plugins {
    id 'java'
    id 'eclipse'
    id 'signing'
    id 'maven-publish'
}

version = "1.2.2"
group = "dev.array21"

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {}

dependencies {}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
	repositories {
		maven {
			name = "ossrh"
			url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = findProperty('ossrhUsername')
				password = findProperty('ossrhPassword')
			}
		}
		
		// Uncomment this only if your version ends with -SNAPSHOT, you'll get a 400 otherwhise.
		/*
		maven {
			name = "ossrhSnapshot"
			url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
			credentials {
				username = findProperty('ossrhUsername')
				password = findProperty('ossrhPassword')
			}
		}*/
	}

	publications {
    	mavenJava(MavenPublication) {
      		from components.java
      		artifact sourcesJar
      		artifact javadocJar
      		
			pom {
		    	name = 'HttpLib'
		    	artifactId = 'httplib'
		    	packaging = 'jar'
		    	description = 'Simple Java HTTP library uitlizing java.net.HttpUrlConnection'
		    	url = 'https://github.com/TobiasDeBruijn/HttpLib'
			
				scm {
					connection = 'scm:git:git://github.com/TobiasDeBruijn/HttpLib.git'
					developerConnection = 'scm:git:ssh://github.com/TobiasDeBruijn/HttpLib.git'
					url = 'https://github.com/TobiasDeBruijn/HttpLib'
				}
		
		    	licenses {
		      		license {
		        		name = 'MIT Licence'
		        		url = 'https://github.com/TobiasDeBruijn/HttpLib/blob/master/LICENSE'
		      		}
	    		}
		
		    	developers {
		      		developer {
		        		id = 'tobiasdebruijn'
		        		name = 'Tobias de Bruijn'
		        		email = 't.debruijn@array21.dev'
		      		}
		    	}
		    }
    	}
	}
}

signing {
	sign publishing.publications.mavenJava
}